# $Id: PKGBUILD 60970 2011-12-19 21:33:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.0
pkgrel=2
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'pango' 'xorg-xsetroot')
install=dwm.install
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
  config.h
  dwm.desktop
  # http://dwm.suckless.org/patches/dwm-6.0-statusallmons.diff
  dwm-6.0-statusallmons_josh.diff
  dwm-6.0-panel_padding.patch
  dwm-6.0-deck.diff
  dwm-6.0-statuscolors.diff
  )

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i ../dwm-6.0-panel_padding.patch
  patch -p1 -i ../dwm-6.0-deck.diff
  patch -p1 -i ../dwm-6.0-statuscolors.diff
  patch -p1 -i ../dwm-6.0-statusallmons_josh.diff
  cp $srcdir/config.h config.h
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
md5sums=('8bb00d4142259beb11e13473b81c0857'
         '824a5cd3a2e9301707510e2d14544c32'
         '939f403a71b6e85261d09fc3412269ee'
         '2906f2bf39b0b34d2791aae17d56aa40'
         'e4631fba6d4a255310b136da3118be3a'
         '168825c2b07ca8acb88bef9602b3de5e'
         'cfb6d7785b35a80e0f44a48a5036af8b')
md5sums=('8bb00d4142259beb11e13473b81c0857'
         '824a5cd3a2e9301707510e2d14544c32'
         '939f403a71b6e85261d09fc3412269ee'
         '2906f2bf39b0b34d2791aae17d56aa40'
         'e4631fba6d4a255310b136da3118be3a'
         '168825c2b07ca8acb88bef9602b3de5e'
         'cfb6d7785b35a80e0f44a48a5036af8b')
